import org.apache.tools.ant.taskdefs.condition.Os

tasks.addRule('Pattern: yarn<Command>') { String taskName ->
    if (taskName.startsWith('yarn')) {
        def yarn = Os.isFamily(Os.FAMILY_WINDOWS) ? "yarn.cmd" : "yarn"
        def command = (taskName - 'yarn').uncapitalize()

        task(taskName, type: Exec) {
            commandLine yarn, command
        }
    }
}

/*
 * Further configure rule tasks, e.g. with inputs and outputs.
 */

yarnInstall {
    description 'Use Yarn to install the Node.js dependencies.'
    group = 'Node'

    inputs.files 'package.json', 'yarn.lock'
    outputs.dir 'node_modules'
}

yarnBuild {
    description 'Use Yarn to build the Node.js application.'
    group = 'Node'

    dependsOn yarnInstall

    inputs.dir 'config'
    inputs.dir 'node_modules'
    inputs.dir 'public'
    inputs.dir 'scripts'
    inputs.dir 'src'

    outputs.dir 'build'
}

yarnLint {
    description 'Let Yarn run the linter to check for style issues.'
    group = 'Node'

    dependsOn yarnInstall
}

/*
 * Resemble the Java plugin tasks for convenience.
 */

task build(dependsOn: [yarnBuild, yarnLint])
task check(dependsOn: yarnLint)

task clean(type: Delete) {
    delete 'build'
    delete 'node_modules'
    delete 'yarn-error.log'
}
